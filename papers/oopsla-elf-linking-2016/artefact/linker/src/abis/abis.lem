(** The [abis] module is the top-level module for all ABI related code, including
  * some generic functionality that works across all ABIs, and a primitive attempt
  * at abstracting over ABIs for purposes of linking.
  *)

open import Basic_classes
open import Bool
open import Num
open import Maybe
open import List
open import Set
import Map
open import String
open import Show
open import Assert_extra
open import Error
open import Missing_pervasives

open import Elf_file
open import Elf_header
open import Elf_interpreted_section
open import Elf_relocation
open import Elf_symbol_table
open import Elf_program_header_table
open import Elf_section_header_table
open import Memory_image

open import Abi_amd64
open import Abi_amd64_relocation

open import Abi_aarch64_le
open import Abi_aarch64_relocation

open import Abi_power64
open import Abi_power64_relocation

open import Gnu_ext_abi

open import Abi_classes
open import Abi_utilities
open import Elf_types_native_uint

open import Memory_image_orderings

(** Relocation operators and their validity on a given platform *)

val is_valid_abi_aarch64_relocation_operator : relocation_operator -> bool
let is_valid_abi_aarch64_relocation_operator op =
  match op with
    | Page -> true
    | G -> true
    | GDat -> true
    | GLDM -> true
    | DTPRel -> true
    | GTPRel -> true
    | TPRel -> true
    | GTLSDesc -> true
    | Delta -> true
    | LDM -> true
    | TLSDesc -> true
    | Indirect -> true
    | _ -> false
  end
  
val is_valid_abi_aarch64_relocation_operator2 : relocation_operator2 -> bool
let is_valid_abi_aarch64_relocation_operator2 op =
  match op with
    | GTLSIdx -> true
  end

val is_valid_abi_amd64_relocation_operator : relocation_operator -> bool
let is_valid_abi_amd64_relocation_operator op =
  match op with
    | Indirect -> true
    | _ -> false (* XXX: not sure about this? *)
  end
  
val is_valid_abi_amd64_relocation_operator2 : relocation_operator2 -> bool
let is_valid_abi_amd64_relocation_operator2 op =
  match op with
    | _ -> false
  end

val is_valid_abi_power64_relocation_operator : relocation_operator -> bool
let is_valid_abi_power64_relocation_operator op = false (* TODO *)

val is_valid_abi_power64_relocation_operator2 : relocation_operator2 -> bool
let is_valid_abi_power64_relocation_operator2 op =
  match op with
    | _ -> false
  end

(** Misc. ABI related stuff *)

type any_abi_feature = Amd64AbiFeature of amd64_abi_feature any_abi_feature
                     | Aarch64LeAbiFeature of aarch64_le_abi_feature

val anyAbiFeatureCompare : any_abi_feature -> any_abi_feature -> Basic_classes.ordering
let anyAbiFeatureCompare f1 f2 = 
    match (f1, f2) with
        (Amd64AbiFeature(af1), Amd64AbiFeature(af2)) -> compare af1 af2
       |(Amd64AbiFeature(_), _) -> LT
       |(Aarch64LeAbiFeature(af1), Amd64AbiFeature(af2)) -> GT
       |(Aarch64LeAbiFeature(af1), Aarch64LeAbiFeature(af2)) -> compare af1 af2
    end

val anyAbiFeatureTagEquiv : any_abi_feature -> any_abi_feature -> bool
let anyAbiFeatureTagEquiv f1 f2 = 
    match (f1, f2) with
        (Amd64AbiFeature(af1), Amd64AbiFeature(af2)) -> abiFeatureTagEquiv af1 af2
       |(Amd64AbiFeature(_), _) -> false
       |(Aarch64LeAbiFeature(af1), Amd64AbiFeature(af2)) -> false
       |(Aarch64LeAbiFeature(af1), Aarch64LeAbiFeature(af2)) -> abiFeatureTagEquiv af1 af2
    end

instance (Ord any_abi_feature)
    let compare = anyAbiFeatureCompare
    let (<) = fun f1 -> (fun f2 -> (anyAbiFeatureCompare f1 f2 = LT))
    let (<=) = fun f1 -> (fun f2 -> Set.member (anyAbiFeatureCompare f1 f2) {LT; EQ})
    let (>) = fun f1 -> (fun f2 -> (anyAbiFeatureCompare f1 f2 = GT))
    let (>=) = fun f1 -> (fun f2 -> Set.member (anyAbiFeatureCompare f1 f2) {GT; EQ})
end

instance (AbiFeatureTagEquiv any_abi_feature)
    let abiFeatureTagEquiv = anyAbiFeatureTagEquiv
end

let make_elf64_header data osabi abiv ma t entry shoff phoff phnum shnum shstrndx =
      <| elf64_ident    = [elf_mn_mag0; elf_mn_mag1; elf_mn_mag2; elf_mn_mag3; 
                           unsigned_char_of_natural elf_class_64; 
                           unsigned_char_of_natural data;
                           unsigned_char_of_natural elf_ev_current;
                           unsigned_char_of_natural osabi;
                           unsigned_char_of_natural abiv;
                           unsigned_char_of_natural 0;
                           unsigned_char_of_natural 0;
                           unsigned_char_of_natural 0;
                           unsigned_char_of_natural 0;
                           unsigned_char_of_natural 0;
                           unsigned_char_of_natural 0;
                           unsigned_char_of_natural 0]
       ; elf64_type     = elf64_half_of_natural t
       ; elf64_machine  = elf64_half_of_natural ma
       ; elf64_version  = elf64_word_of_natural elf_ev_current
       ; elf64_entry    = elf64_addr_of_natural entry
       ; elf64_phoff    = elf64_off_of_natural phoff
       ; elf64_shoff    = elf64_off_of_natural shoff
       ; elf64_flags    = elf64_word_of_natural 0
       ; elf64_ehsize   = elf64_half_of_natural 64
       ; elf64_phentsize= elf64_half_of_natural 56
       ; elf64_phnum    = elf64_half_of_natural phnum
       ; elf64_shentsize= elf64_half_of_natural 64
       ; elf64_shnum    = elf64_half_of_natural shnum
       ; elf64_shstrndx = elf64_half_of_natural shstrndx
       |>
       
val phdr_flags_from_section_flags : natural -> string -> natural
let phdr_flags_from_section_flags section_flags sec_name =
    let flags = natural_lor elf_pf_r (natural_lor 
        (if flag_is_set shf_write section_flags then elf_pf_w else 0)
        (if flag_is_set shf_execinstr section_flags then elf_pf_x else 0))
    in
    (*let _ = errln ("Phdr flags of section " ^ sec_name ^ "(ELF section flags 0x " ^ 
        (hex_string_of_natural section_flags) ^ ") are 0x" ^ (hex_string_of_natural flags))
    in*)
    flags

val phdr_is_writable : natural -> bool
let phdr_is_writable flags =
    (natural_land flags elf_pf_w) = elf_pf_w

type can_combine_flags_fn = set natural -> maybe natural

(* FIXME: lift this to a personality function of the GNU linker? 
 * Not sure really... need to try some other linkers. *)
val load_can_combine_flags : can_combine_flags_fn
let load_can_combine_flags flagsets = 
    (* The GNU linker happily adds a .rodata section to a RX segment,
     * but not to a RW segment. So the only clear rule is: if any is writable,
     * all must be writable. *)
    let flagslist = Set_extra.toList flagsets
    in
    let union_flags = List.foldl natural_lor 0 flagslist
    in
    if List.any phdr_is_writable flagslist
    then
        if List.all phdr_is_writable flagslist then Just union_flags
        else Nothing
    else
        Just union_flags
        
val tls_can_combine_flags : can_combine_flags_fn
let tls_can_combine_flags flagsets = Just (List.foldl natural_lor 0 (Set_extra.toList flagsets))

let maybe_extend_phdr phdr isec can_combine_flags =
    let new_p_type = natural_of_elf64_word phdr.elf64_p_type
    in
    let this_section_phdr_flags = phdr_flags_from_section_flags isec.elf64_section_flags isec.elf64_section_name_as_string
    in
    let maybe_extended_flags = can_combine_flags { this_section_phdr_flags; natural_of_elf64_word phdr.elf64_p_flags }
    in
    if maybe_extended_flags = Nothing then (*let _ = errln "flag mismatch" in*) Nothing
    else let new_p_flags = match maybe_extended_flags with Just flags -> flags | _ -> failwith "impossible" end
    in
    (* The new filesz is the file end offset of this section,
     * minus the existing file start offset of the phdr. 
     * Check that the new section begins after the old offset+filesz. *)
    if isec.elf64_section_offset < (natural_of_elf64_off phdr.elf64_p_offset) + (natural_of_elf64_xword phdr.elf64_p_filesz)
    then (*let _ = errln "offset went backwards" in*) Nothing
    else 
    let new_p_filesz = natural_of_elf64_xword phdr.elf64_p_filesz + (if isec.elf64_section_type = sht_progbits then isec.elf64_section_size else 0)
    in 
    (* The new memsz is the virtual address end address of this section,
     * minus the existing start vaddr of the phdr. 
     * Check that the new section begins after the old vaddr+memsz. *)
    if isec.elf64_section_addr < (natural_of_elf64_addr phdr.elf64_p_vaddr) + (natural_of_elf64_xword phdr.elf64_p_memsz)
    then (*let _ = errln "vaddr went backwards" in*) Nothing
    else 
    let new_p_memsz = natural_of_elf64_xword phdr.elf64_p_memsz + isec.elf64_section_size
    in
    let (one_if_zero : natural -> natural) = fun n -> if n = 0 then 1 else n
    in
    let new_p_align =  lcm (one_if_zero (natural_of_elf64_xword phdr.elf64_p_align)) (one_if_zero isec.elf64_section_align)
    in
    Just
      <| elf64_p_type   = elf64_word_of_natural new_p_type
       ; elf64_p_flags  = elf64_word_of_natural new_p_flags
       ; elf64_p_offset = phdr.elf64_p_offset
       ; elf64_p_vaddr  = phdr.elf64_p_vaddr
       ; elf64_p_paddr  = phdr.elf64_p_paddr
       ; elf64_p_filesz = elf64_xword_of_natural new_p_filesz
       ; elf64_p_memsz  = elf64_xword_of_natural new_p_memsz
       ; elf64_p_align  = elf64_xword_of_natural new_p_align
       |>

let make_new_phdr isec t maxpagesize commonpagesize =
    let rounded_down_offset = fun isec -> round_down_to commonpagesize isec.elf64_section_offset
    in
    let offset_round_down_amount = fun isec -> isec.elf64_section_offset - (rounded_down_offset isec)
    in
    let rounded_down_vaddr = fun isec -> round_down_to commonpagesize isec.elf64_section_addr
    in
    let vaddr_round_down_amount = fun isec -> isec.elf64_section_addr - (rounded_down_vaddr isec)
    in
  <| elf64_p_type   = elf64_word_of_natural t (** Type of the segment *)
   ; elf64_p_flags  = elf64_word_of_natural (phdr_flags_from_section_flags isec.elf64_section_flags isec.elf64_section_name_as_string) (** Segment flags *)
   ; elf64_p_offset = elf64_off_of_natural (rounded_down_offset isec) (** Offset from beginning of file for segment *)
   ; elf64_p_vaddr  = elf64_addr_of_natural (rounded_down_vaddr isec) (** Virtual address for segment in memory *)
   ; elf64_p_paddr  = elf64_addr_of_natural 0 (** Physical address for segment *)
   ; elf64_p_filesz = elf64_xword_of_natural (if isec.elf64_section_type = sht_nobits then 0 else isec.elf64_section_size + (offset_round_down_amount isec)) (** Size of segment in file, in bytes *)
   ; elf64_p_memsz  = elf64_xword_of_natural (isec.elf64_section_size + (vaddr_round_down_amount isec)) (** Size of segment in memory image, in bytes *)
   ; elf64_p_align  = elf64_xword_of_natural (* isec.elf64_section_align *) maxpagesize (** Segment alignment memory for memory and file *)
   |>

val make_load_phdrs : forall 'abifeature. natural -> natural -> annotated_memory_image 'abifeature -> list elf64_interpreted_section -> list elf64_program_header_table_entry
let make_load_phdrs maxpagesize commonpagesize img section_pairs_bare_sorted_by_address = 
    (* accumulate sections into the phdr *)
    let rev_list = List.foldl (fun accum_phdr_list -> (fun isec -> (
        (* Do we have a current phdr? *)
        match accum_phdr_list with
            [] -> (* no, so make one *)
                (*let _ = errln ("Starting the first LOAD phdr for section " ^ isec.elf64_section_name_as_string)
                in*)
                [make_new_phdr isec elf_pt_load maxpagesize commonpagesize]
            | current_phdr :: more -> 
                (* can we extend it with the current section? *)
                match maybe_extend_phdr current_phdr isec load_can_combine_flags with
                    Nothing -> 
                        (*let _ = errln ("Starting new LOAD phdr for section " ^ isec.elf64_section_name_as_string)
                        in*)
                        (make_new_phdr isec elf_pt_load maxpagesize commonpagesize) :: current_phdr :: more
                    | Just phdr -> phdr :: more
                end
        end
    ))) [] (List.filter (fun isec -> flag_is_set shf_alloc isec.elf64_section_flags
        && not (flag_is_set shf_tls isec.elf64_section_flags)) section_pairs_bare_sorted_by_address)
    in
    (*let _ = errln "Successfully made phdrs"
    in*)
    List.reverse rev_list

val tls_extend: forall 'abifeature. abi 'abifeature -> abi 'abifeature
let tls_extend a = 
   <| is_valid_elf_header = a.is_valid_elf_header
    ; make_elf_header     = a.make_elf_header
    ; reloc               = a.reloc
    ; section_is_special  = a.section_is_special
    ; section_is_large    = a.section_is_large
    ; maxpagesize         = a.maxpagesize
    ; minpagesize         = a.minpagesize
    ; commonpagesize      = a.commonpagesize
    ; symbol_is_generated_by_linker = a.symbol_is_generated_by_linker
    ; make_phdrs          = fun maxpagesize -> fun commonpagesize -> fun file_type -> fun img -> fun section_pairs_bare_sorted_by_address -> (
        let rev_list = List.foldl (fun accum_phdr_list -> (fun isec -> (
        match accum_phdr_list with
            [] ->
                let _ = errln "Making a new TLS program header" in
                [make_new_phdr isec elf_pt_tls maxpagesize commonpagesize]
            | current_phdr :: more -> 
                match maybe_extend_phdr current_phdr isec tls_can_combine_flags with
                    Nothing -> 
                        (make_new_phdr isec elf_pt_tls maxpagesize commonpagesize) :: current_phdr :: more
                    | Just phdr -> phdr :: more
                end
        end
        ))) [] (List.filter (fun isec -> flag_is_set shf_alloc isec.elf64_section_flags
            && flag_is_set shf_tls isec.elf64_section_flags) section_pairs_bare_sorted_by_address)
        in
        (a.make_phdrs maxpagesize commonpagesize file_type img section_pairs_bare_sorted_by_address) ++ (List.reverse rev_list)
    )
    ; max_phnum           = 1 + a.max_phnum
    ; guess_entry_point   = a.guess_entry_point
    ; pad_data            = a.pad_data
    ; pad_code            = a.pad_code
    ; generate_support    = a.generate_support
    ; concretise_support  = a.concretise_support
    ; get_reloc_symaddr   = a.get_reloc_symaddr
    |>

(* We use these snappily-named functions in relocation calculations. *)

val make_default_phdrs : forall 'abifeature. natural -> natural -> natural (* file type *) -> annotated_memory_image 'abifeature -> list elf64_interpreted_section -> list elf64_program_header_table_entry
let make_default_phdrs maxpagesize commonpagesize t img section_pairs_bare_sorted_by_address = 
    (* FIXME: do the shared object and dyn. exec. stuff too *)
    make_load_phdrs maxpagesize commonpagesize img section_pairs_bare_sorted_by_address

val find_start_symbol_address : forall 'abifeature. Ord 'abifeature, AbiFeatureTagEquiv 'abifeature => annotated_memory_image 'abifeature -> maybe natural
let find_start_symbol_address img = 
    (* Do we have a symbol called "_start"? *)
    let all_defs = Memory_image_orderings.defined_symbols_and_ranges img
    in
    let get_entry_point = (fun (maybe_range, symbol_def) -> 
        if symbol_def.def_symname = "_start"
        then Just (maybe_range, symbol_def) 
        else Nothing
    )
    in
    let all_entry_points = List.mapMaybe get_entry_point all_defs
    in
    match all_entry_points with
        [(maybe_range, symbol_def)] ->
            match maybe_range with
                Just (el_name, (el_off, len)) -> 
                    match Map.lookup el_name img.elements with
                        Nothing -> failwith ("_start symbol defined in nonexistent element `" ^ el_name ^ "'")
                        | Just el_rec -> 
                            match el_rec.startpos with
                                Nothing -> (*let _ = Missing_pervasives.errln "warning: saw `_start' in element with no assigned address" in *)Nothing
                                | Just x -> (* success! *) Just (x + el_off)
                            end
                    end
                | _ -> (*let _ = Missing_pervasives.errln "warning: `_start' symbol with no range" in*) Nothing
            end
        | [] -> (* no _start symbol *) Nothing
        | _ -> (*let _ = Missing_pervasives.errln ("warning: saw multiple `_start' symbols: " ^
            (let (ranges, defs) = unzip all_entry_points in show ranges)) in *)Nothing
    end

val pad_zeroes : natural -> list byte
let pad_zeroes n = replicate n (byte_of_natural 0)

val pad_0x90 : natural -> list byte
let pad_0x90 n = replicate n (byte_of_natural (9 * 16))

(* null_abi captures ABI details common to all ELF-based, System V-based systems.
 * HACK: for now, specialise to 64-bit ABIs. *)
val null_abi : abi any_abi_feature 
let (null_abi : abi any_abi_feature) = <|
      is_valid_elf_header = is_valid_elf64_header
    ; make_elf_header = make_elf64_header elf_data_2lsb elf_osabi_none 0 elf_ma_none
    ; reloc = noop_reloc
    ; section_is_special = elf_section_is_special
    ; section_is_large = (fun s -> (fun f -> false))
    ; maxpagesize = 2 * 256 * 4096 (* 2MB; bit of a guess, based on gdb and prelink code *)
    ; minpagesize = 1024 (* bit of a guess again *)
    ; commonpagesize = 4096
    ; symbol_is_generated_by_linker = (fun symname -> symname = "_GLOBAL_OFFSET_TABLE_")
    ; make_phdrs = make_default_phdrs
    ; max_phnum = 2
    ; guess_entry_point = find_start_symbol_address
    ; pad_data = pad_zeroes
    ; pad_code = pad_zeroes
    ; generate_support = (* fun _ -> *) fun _ -> get_empty_memory_image ()
    ; concretise_support = fun img -> img
    ; get_reloc_symaddr = default_get_reloc_symaddr
    |>

val got_entry_ordering : (string * maybe symbol_definition) -> (string * maybe symbol_definition) -> Basic_classes.ordering
let got_entry_ordering (s1, md1) (s2, md2) = compare s1 s2 (* FIXME *)

let is_ifunc_def = fun maybe_def -> 
match maybe_def with
 Nothing -> false
 | Just d -> get_elf64_symbol_type d.def_syment = stt_gnu_ifunc
end

let amd64_reloc_needs_got_slot =  fun symref -> fun rr -> fun maybe_def -> 
    if (get_elf64_relocation_a_type rr.ref_relent IN {
        r_x86_64_got32; r_x86_64_gotpcrel; r_x86_64_gottpoff; r_x86_64_gotoff64; r_x86_64_gotpc32 (* ; r_x86_64_gotpc32_tlsdesc *)
    }) then 
       true
    else if is_ifunc_def maybe_def
         then
         (* This reference is bound to a STT_GNU_IFUNC definition. 
          * What now needs to happen is as follows.
          * - we ensure that a GOT entry is generated for this symbol (we do this here);
          * - we ensure that a PLT entry (specifically .iplt) is generated for the symbol (Below);
          * - on making the GOT, we also generate a .rela.plt relocation record covering the GOT slot;
          * - when applying the relocation, of whatever kind, the address of the PLT slot 
          *      is the address input to the calculation
          * - the code marked by the STT_GNU_IFUNC symbol definition is not the function
          *      to call; it's the function that calculates the address of the function to call!
          *      this becomes the addend of the R_X86_64_IRELATIVE Elf64_Rela marking the GOT slot
          * - note that for static linking, the GOT is usually pre-filled (cf. dynamically when it is filled by JUMP_SLOT relocs).
          *      ... but our GOT entries *must* have corresponding R_X86_64_IRELATIVEs generated
          *)
          true
    else false


let amd64_reloc_needs_plt_slot (symref : symbol_reference_and_reloc_site) rr maybe_def =
    if (get_elf64_relocation_a_type rr.ref_relent IN {
        r_x86_64_plt32      (* NOTE: when generating shared libs, it starts to matter
                             * where the definition is -- anything that is locally interposable
                             * or undefined will need a slot. See amd64_get_reloc_symaddr. *)
    }) then
       true
    else if is_ifunc_def maybe_def 
         then
         true
    else
        (* not a PLT ref *)
        false

let amd64_find_got_label_and_element img = 
    match Map.lookup ".got" img.elements with
        Nothing -> (* got no GOT? okay... *) Nothing
        | Just got_el -> 
            (* Find the GOT tag. *)
            let tags_and_ranges = Multimap.lookupBy Memory_image_orderings.tagEquiv (AbiFeature(Amd64AbiFeature(Abi_amd64.GOT([])))) img.by_tag
            in
            match tags_and_ranges with
                [] -> failwith "error: GOT element but no ABI feature GOT tag"
                | [(AbiFeature(Amd64AbiFeature(Abi_amd64.GOT(l))), _)] -> Just (l, got_el)
                | _ -> failwith ("multiple GOT elements/tags")
            end
    end

let amd64_find_plt_label_and_element img = 
    match Map.lookup ".plt" img.elements with
        Nothing -> (* got no PLT? okay... *) Nothing
        | Just plt_el -> 
            (* Find the PLT tag. *)
            let tags_and_ranges = Multimap.lookupBy Memory_image_orderings.tagEquiv (AbiFeature(Amd64AbiFeature(Abi_amd64.PLT([])))) img.by_tag
            in
            match tags_and_ranges with
                [] -> failwith "error: PLT element but no ABI feature PLT tag"
                | [(AbiFeature(Amd64AbiFeature(Abi_amd64.PLT(l))), _)] -> Just (l, plt_el)
                | _ -> failwith ("multiple PLT elements/tags")
            end
    end

let got_slot_index_for_symname symname got_label =
    List.findIndex (fun (s, _) -> s = symname) got_label

val amd64_get_reloc_symaddr : symbol_definition -> annotated_memory_image any_abi_feature -> maybe reloc_site -> natural
let amd64_get_reloc_symaddr the_input_def output_img maybe_reloc = 
    (* The default implementation simply looks up a "matching" symbol in the output image
     * and calculates its address.
     * 
     * That's normally fine, even for via-GOT references since their calculations don't
     * use the symaddr. For via-PLT references, we need to use the PLT slot address.
     * HMM. Isn't this duplicating the role of functions like amd64_plt_slot_addr?
     
     * Recall that we created this get_reloc_symaddr mechanism to deal with IFUNC symbols. 
     * With an IFUNC symbol, we reference it simply using a PC32 relocation, but the address
     * that gets filled in isn't the IFUNC address; it's the corresponding PLT slot. 
     * HMM: does this happen for other PC32 references? If so, we'll need this mechanism
     * there. And it certainly does, because relocatable object code never uses PLT32
     * relocs. 
     * 
     * I had previously tried to handle this issue in mark_fate_of_relocs, using the
     * 1-argument ApplyReloc(_) and MakePIC to encode the "replacement". But at that stage, 
     * which is ABI-independent and happens before address assignment?, we can't know enough.
     *)
    (* match bound_def_in_input with
        Nothing -> 0
        | Just the_input_def -> *)
        if is_ifunc_def (Just(the_input_def))
        then
            (* We need to return the address of the "matching" PLT slot.
             * PLT label entries are (symname, maybe_def, content_fn). *)
            match amd64_find_plt_label_and_element output_img with
                Nothing -> failwith "error: ifunc but no PLT"
                | Just (l, plt_el) ->
                match List.findIndex (fun (symname, _, _) -> symname = the_input_def.def_symname) l with
                    (* FIXME: using symnames seems wrong *)
                    Just idx -> 
                        match plt_el.startpos with
                            Just addr -> addr + (naturalFromNat idx) * 16 (* size of a PLT entry *)
                            | Nothing -> failwith "error: PLT has no address assigned"
                        end
                    | Nothing -> 0
                end
            end
        else default_get_reloc_symaddr the_input_def output_img maybe_reloc
    (* end *)

(* *)
val amd64_generate_support : (* list (list reloc_site_resolution) -> *) list (string * annotated_memory_image any_abi_feature) -> annotated_memory_image any_abi_feature
let amd64_generate_support (* reloc_resolution_lists *) input_fnames_and_imgs = 
    (* We generate a basic GOT. At the moment we can only describe the GOT
     * contents abstractly, not as its binary content, because addresses
     * have not yet been fixed. 
     * 
     * To do this, we create a set of Abi_amd64.GOTEntry records, one for
     * each distinct symbol that is referenced by one or more GOT-based relocations.
     * To enumerate these, we look at all the symbol refs in the image.
     *)
    let (fnames, input_imgs) = unzip input_fnames_and_imgs
    in
    let tags_and_ranges_by_image = List.mapi (fun i -> fun (fname, img) ->
        (i, fname, Multimap.lookupBy Memory_image_orderings.tagEquiv (SymbolRef(null_symbol_reference_and_reloc_site)) img.by_tag)
    ) input_fnames_and_imgs
    in
    let refs_via_got = list_concat_map (fun (i, fname, tags_and_ranges) -> List.mapMaybe (fun (tag, maybe_range) -> match tag with
         SymbolRef(symref) -> 
            (* Is this ref a relocation we're going to apply, and does it reference the GOT? *)
            match (symref.maybe_reloc, symref.maybe_def_bound_to) with
                (Nothing, _) -> Nothing
                | (Just rr, Just(ApplyReloc, maybe_def)) ->
                    if amd64_reloc_needs_got_slot symref rr maybe_def then
                        (*let _ = errln ("Saw a via-GOT symbol reference: to `" ^ symref.ref.ref_symname ^ "' coming from linkable " ^ (show i) ^ " (" ^ 
                            fname ^ "), logically from section " ^ (show rr.ref_src_scn)) in *)
                        Just (symref.ref.ref_symname, maybe_def) 
                    else Nothing
                | (Just rr, Just(MakePIC, maybe_def)) -> failwith "FIXME: PIC support please"
            end
         | _ -> failwith "impossible: reloc site tag is not a SymbolRef"
    end) tags_and_ranges) tags_and_ranges_by_image
    in
    let (symnames, maybe_defs) = unzip refs_via_got
    in
    (*let _ = errln ("GOT includes defs with names: " ^ (show (Set_extra.toList (Set.fromList symnames))))
    in*)
    let got_pairs_set = Set.fromList refs_via_got
    in
    let got_defs_set = Set.fromList maybe_defs
    in
    (* This is where we fix the order of the GOT entries. *)
    let got_pairs_list = Set_extra.toList got_pairs_set
    in
    let got_idx_and_maybe_def_by_symname_map = Map.fromList (mapi (fun slot_idx -> fun (symname, maybe_def) -> (symname, (slot_idx, maybe_def))) got_pairs_list)
    in
    let got_ifunc_set = { maybe_d | forall (maybe_d IN got_defs_set) | is_ifunc_def maybe_d }
    in
    (* Quirk: what if we have the same def appearing under two different symnames?
     * This shouldn't happen, at present. 
     * What if we have the same symname related to two different defs? This also 
     * shouldn't happen, because only global symbols go in the GOT, so we don't have
     * to worry about local symbols with the same name as another symbol. But still, it 
     * could plausibly happen in some situations with weird symbol visibilities or binding. *)
    (* if Set.size pairs_set <> Set.size defs_set then
        failwith "something quirky going on with GOT symbol defs and their names"
    else *)
(*    let name_def_pairs = List.foldl (fun acc -> fun (idx, symname, (maybe_range, rr)) -> 
        Set.insert (
        
                symname, (match rr.maybe_def_bound_to with
        Map.lookup symname acc with 
            Nothing -> [item]
            | Just l -> item :: l
        end) acc) {} refs_via_got
    in *)
    let got_nentries = naturalFromNat (Set.size got_pairs_set)
    in
    let got_entrysize = 8
    in
    (* We also need a PLT... sort of. We need a way to resolve via-PLT relocs. 
     * But we might do so without actually creating a (non-zero-length) PLT. 
     * Again, this is to accommodate the sorts of optimisations the GNU linker does.
     * 
     * Note that every PLT entry has a corresponding GOT entry. Here we are simply
     * enumerating the via-PLT relocs that imply a PLT entry. We look their GOT
     * slots up later, by symbol name. *)
    let refs_via_plt = list_concat_map (fun (i, fname, tags_and_ranges) -> List.mapMaybe (fun (tag, maybe_range) -> match tag with
         SymbolRef(symref) -> 
            (* Is this ref a relocation we're going to apply, and does it reference the GOT? *)
            match (symref.maybe_reloc, symref.maybe_def_bound_to) with
                (Nothing, _) -> Nothing
                | (Just rr, Just(ApplyReloc, maybe_def)) ->
                    if amd64_reloc_needs_plt_slot symref rr maybe_def 
                    then 
                        (*let _ = if is_ifunc_def maybe_def then
                         (* we ensure that a PLT entry (specifically .iplt) is generated for the symbol *)
                         errln ("Saw a reference to IFUNC symbol `" ^ symref.ref.ref_symname ^ "'; ref is coming from linkable " ^ (show i) ^ " (" ^ 
                            fname ^ "), relent idx " ^ (show rr.ref_rel_idx) ^ " logically from section " ^ (show rr.ref_src_scn) ) 
                        else
                        errln ("Saw a via-PLT symbol reference: to `" ^ symref.ref.ref_symname ^ "' coming from linkable " ^ (show i) ^ " (" ^ 
                            fname ^ "), relent idx " ^ (show rr.ref_rel_idx) ^ " logically from section " ^ (show rr.ref_src_scn) ^ 
                            match maybe_def with Just _ -> ", with definition" | Nothing -> ", not bound to anything" end
                            )
                        in*)
                        Just(symref.ref.ref_symname, maybe_def) 
                    else Nothing
                | (Just rr, Just(MakePIC, maybe_def)) -> failwith "FIXME: PIC support please"
            end
         | _ -> failwith "impossible: reloc site tag is not a SymbolRef"
    end) tags_and_ranges) tags_and_ranges_by_image
    in
    (*let _ = errln ("Saw " ^ (show (length refs_via_plt)) ^ " relocations of a via-PLT type")
    in*)
    (* account for the optimisations we did on GOT slots *)
    let refs_via_plt_having_got_slot = List.mapMaybe (fun (symname, _) -> 
        match Map.lookup symname got_idx_and_maybe_def_by_symname_map with
            Just(idx, maybe_d) -> Just (symname, idx, maybe_d)
            | Nothing -> Nothing
            end
        ) refs_via_plt
    in
    (*let _ = errln ("Saw " ^ (show (length refs_via_plt_having_got_slot)) ^ " relocations of a via-PLT type where we instantiated a GOT slot for the symbol")
    in*)
    let (plt_symnames, plt_got_idxs, plt_ref_bound_to_maybe_defs) = unzip3 refs_via_plt_having_got_slot
    in
    let plt_symnames_excluding_header = (Set_extra.toList (Set.fromList plt_symnames))
    in
    (*let _ = errln ("PLT symnames: " ^ (show plt_symnames_excluding_header))
    in*)
    let n_iplt_entries = Set.size got_ifunc_set
        (* The content of the IPLT entries depends on the address assignment of GOT slots
         * and the IFUNCs that they reference. We need to reserve space for them here, though. *)
    in
    (*let _ = errln ("We think there should be " ^ (show n_iplt_entries) ^ " PLT entries due to references to IFUNC symbols")
    in*)
    (* let got_entries_referencing_functions =  (List.filter (fun (symname, maybe_def) -> 
            match def with
                Just d -> d.def_syment
                | Nothing -> false
            end) refs_via_got)
    in *)
    let plt_needs_header_entry = (List.length plt_symnames_excluding_header) > n_iplt_entries
    in
    (*let _ = errln ("PLT needs header entry? " ^ (show plt_needs_header_entry))
    in*)
    let total_n_plt_entries = (if plt_needs_header_entry then 1 else 0) + (Missing_pervasives.length plt_symnames_excluding_header)
    in
    (*let _ = errln ("PLT total entry count: " ^ (show total_n_plt_entries))
    in*)
    let new_by_range = {
        (Just(".plt", (0, 16 * total_n_plt_entries)), AbiFeature(Amd64AbiFeature(Abi_amd64.PLT(
                 mapi (fun plt_entry_idx_not_counting_header -> (fun symname ->
                    (* We want to label the PLT entry with a function that 
                     * - accepts the PLT base address, the GOT base address and the GOT slot number;
                     * - closure-captures whatever else it needs (whether we're inserting a PLT header);
                     * - yields the *full contents of the PLT entry* before relocation. 
                     * - recall that PLT entries might be "header" (the special one at the start), 
                     *      "normal" (to be relocated with R_X86_64_JUMP_SLOT)
                     *   or "irelative" (to be relocated with R_X86_64_IRELATIVE).
                     *    Q. Why are R_X86_64_JUMP_SLOT necessary?
                     *       The PLT entries are doing relative addressing, and
                     *       the offset to the GOT entry is known at link time,
                     *       so the linker should be able to fill them in. In
                     *       fact, it does. HMM. Understand this better. *)
                    (* What is the GOT slot number? *)
                    let (got_slot_idx, maybe_def) = match Map.lookup symname got_idx_and_maybe_def_by_symname_map with
                        Just(idx, maybe_d) -> (naturalFromNat idx, maybe_d)
                        | Nothing -> failwith ("GOT does not contain symbol `" ^ symname ^ "' required by PLT entry")
                        end
                    in
                    (symname, maybe_def, ((fun (got_base_addr : natural) -> fun (plt_base_addr : natural) -> 
                        (* Okay, now we can generate the entry. NOTE that we're lexically still in generate_support,
                         * but we'll be called from concretise_support. The code that generates the header
                         * entry is actually in concretise_support. 
                         * 
                         * If the entry is a normal entry, it looks like
                         * 
                               0x0000000000400410 <+0>:     ff 25 02 0c 20 00       jmpq   *0x200c02(%rip)        # 0x601018 <puts@got.plt>
                               0x0000000000400416 <+6>:     68 00 00 00 00  pushq  $0x0
                               0x000000000040041b <+11>:    e9 e0 ff ff ff  jmpq   0x400400
                         * 
                         * If the entry is an irelative entry, it looks like 
                         * 
                              400350:       ff 25 02 fd 2b 00       jmpq   *0x2bfd02(%rip)        # 6c0058 <_GLOBAL_OFFSET_TABLE_+0x58>
                              400356:       68 00 00 00 00          pushq  $0x0
                              40035b:       e9 00 00 00 00          jmpq   400360 <check_one_fd.part.0>
                         
                         * ... i.e. basically the same but the pushq and jmpq have literal-zero args (they're not used).
                         *)
                        let this_plt_slot_base_addr = plt_base_addr + 16 * ((naturalFromNat plt_entry_idx_not_counting_header) 
                            + if plt_needs_header_entry then 1 else 0)
                        in
                        (*let _ = Missing_pervasives.errln ("PLT slot base address for symname `" ^ symname ^ "': 0x" ^
                            (hex_string_of_natural this_plt_slot_base_addr))
                        in*)
                        let got_slot_addr = got_base_addr + 8 * got_slot_idx
                        in
                        (*let _ = Missing_pervasives.errln ("GOT slot address for symname `" ^ symname ^ "' (idx " ^ (show got_slot_idx) ^ "): 0x" ^
                            (hex_string_of_natural got_slot_addr))
                        in*)
                        let maybe_header_entry_address = if plt_needs_header_entry then Just(plt_base_addr) else Nothing
                        in
                        let content_bytes = 
                        [byte_of_natural 255; byte_of_natural 37] ++ (* offset to the GOT entry, from the *next* instruction start, signed 32-bit LE *)
                            (to_le_signed_bytes 4 (
                                (integerFromNatural got_slot_addr) - (integerFromNatural (this_plt_slot_base_addr + 6))
                            )) ++
                        [byte_of_natural 104] ++ (* plt slot number not including header, 32-bit LE *)
                                 (to_le_unsigned_bytes 4 (integerFromNatural (naturalFromNat plt_entry_idx_not_counting_header))) ++
                        [byte_of_natural 233] ++ (to_le_signed_bytes 4 (
                            if is_ifunc_def maybe_def
                            then 0
                            else match maybe_header_entry_address with
                                Nothing -> failwith "normal PLT entry but no PLT header!"
                                | Just header_entry_address -> (integerFromNatural header_entry_address) - (integerFromNatural (this_plt_slot_base_addr + 16))
                                end
                        ))
                        in
                        (*let _ = errln ("Created a PLT entry consisting of " ^ (show (length content_bytes)) ^ " bytes.")
                        in*)
                        (this_plt_slot_base_addr, content_bytes)
                        (* 
                        match maybe_def with 
                            Nothing -> 0
                            | Just sd -> 
                                match Memory_image_orderings.find_defs_matching sd img with
                                    [] -> failwith ("no matching definitions for PLT entry named " ^ symname)
                                    | [(Just(def_el_name, (def_start, def_len)), d)] -> 
                                        match element_and_offset_to_address (def_el_name, def_start) img with
                                            Nothing -> failwith ("PLT: no address for definition offset in element " ^ def_el_name)
                                            | Just x -> 
                                                let _ = errln ("PLT slot for symbol `" ^ symname ^ 
                                                    "' calculated at (non-PLT) address 0x" ^ (hex_string_of_natural x) ^ 
                                                    " (offset 0x" ^ (hex_string_of_natural def_start) ^ " in element " ^ def_el_name ^ ")")
                                                in
                                                x
                                        end
                                    | _ -> failwith ("multiple matching definitions for PLT entry named " ^ symname)
                                end
                        end
                        *)
                        
                    ) : plt_entry_content_fn any_abi_feature))
                ))
                plt_symnames
            ))) 
        )
    ;   (Just(".plt", (0, 16 * total_n_plt_entries)), FileFeature(ElfSection(1, 
          <| elf64_section_name = 0 (* ignored *)
           ; elf64_section_type = sht_progbits
           ; elf64_section_flags = shf_alloc
           ; elf64_section_addr = 0 (* ignored -- covered by element *)
           ; elf64_section_offset = 0 (* ignored -- will be replaced when file offsets are assigned *)
           ; elf64_section_size = 16 * total_n_plt_entries (* ignored? NO, we use it in linker_script to avoid plumbing through the element record *)
           ; elf64_section_link = 0
           ; elf64_section_info = 0
           ; elf64_section_align = 16
           ; elf64_section_entsize = 16
           ; elf64_section_body = Byte_sequence.empty (* ignored *)
           ; elf64_section_name_as_string = ".plt"
           |>
        )))
        (* For each GOT entry that corresponds to a thread-local symbol, we also need to 
         * generate a relocation record. HMM. These new relocation records are ones we don't 
         * yet have decisions for. That might be a problem. 
         * 
         * In fact, this approach is not appropriate for static linking. Just put the offsets
         * in there when we concretise the GOT. Something like this will be good for 
         * dynamic linking, though. At the moment, creating a SymbolRef at this stage
         * is problematic because it's not in the bindings list. When we generate shared
         * objects, we'll have to revisit that code. *)
        (* (Just(".got", (i * got_entrysize, 8)), SymbolRef( <|
                ref = <| ref_symname = symname
                       ; ref_syment = sd.def_syment
                       ; ref_sym_scn = 0
                       ; ref_sym_idx = 0
                       |>
                ; maybe_def_bound_to = Just(ApplyReloc, Just sd)
                ; maybe_reloc = Just(
                  <|
                        ref_relent  = 
                            <| elf64_ra_offset = elf64_addr_of_natural 0
                             ; elf64_ra_info   = elf64_xword_of_natural r_x86_64_tpoff64
                             ; elf64_ra_addend = elf64_sxword_of_integer 0
                             |>
                      ; ref_rel_scn = 0
                      ; ref_rel_idx = 0
                      ; ref_src_scn = 0
                   |>
                )
              |>)) 
              | forall ((i, symname, sd) IN (Set.fromList (mapMaybei (fun i -> fun (symname, maybe_def) ->
                match maybe_def with Nothing -> Nothing | Just sd -> Just(i, symname, sd) end) refs_via_got)))
              | get_elf64_symbol_type sd.def_syment = stt_tls
     *)
    ;   (Just(".got", (0, got_nentries * got_entrysize)), AbiFeature(Amd64AbiFeature(Abi_amd64.GOT(got_pairs_list))))
    ;   (Just(".got", (0, got_nentries * got_entrysize)), FileFeature(ElfSection(2, 
          <| elf64_section_name = 0 (* ignored *)
           ; elf64_section_type = sht_progbits
           ; elf64_section_flags = natural_lor shf_write shf_alloc
           ; elf64_section_addr = 0 (* ignored -- covered by element *)
           ; elf64_section_offset = 0 (* ignored -- will be replaced when file offsets are assigned *)
           ; elf64_section_size = got_nentries * got_entrysize (* ignored? NO, we use it in linker_script to avoid plumbing through the element record *)
           ; elf64_section_link = 0
           ; elf64_section_info = 0
           ; elf64_section_align = 8
           ; elf64_section_entsize = got_entrysize
           ; elf64_section_body = Byte_sequence.empty (* ignored *)
           ; elf64_section_name_as_string = ".got"
           |>
        )))
    ;   (* FIXME: I've a feeling _GLOBAL_OFFSET_TABLE_ generally doesn't mark the *start* of the GOT; 
         * it's some distance in. What about .got.plt? *)
        (Just(".got", (0, got_nentries * got_entrysize)), SymbolDef(<|
              def_symname = "_GLOBAL_OFFSET_TABLE_"
            ; def_syment =    <| elf64_st_name  = elf64_word_of_natural 0 (* ignored *)
                               ; elf64_st_info  = unsigned_char_of_natural 0 (* FIXME *)
                               ; elf64_st_other = unsigned_char_of_natural 0 (* FIXME *)
                               ; elf64_st_shndx = elf64_half_of_natural 1
                               ; elf64_st_value = elf64_addr_of_natural 0 (* ignored *)
                               ; elf64_st_size  = elf64_xword_of_natural (got_nentries * got_entrysize) (* FIXME: start later, smaller size? zero size? *)
                               |>
            ; def_sym_scn = 1
            ; def_sym_idx = 1
            ; def_linkable_idx = 0
            |>))
    ; (Just(".rela.iplt", (0, 24 (* size of an Elf64_Rela *) * naturalFromNat n_iplt_entries)), FileFeature(ElfSection(3, 
          <| elf64_section_name = 0 (* ignored *)
           ; elf64_section_type = sht_rela
           ; elf64_section_flags = natural_lor shf_alloc shf_info_link
           ; elf64_section_addr = 0 (* ignored -- covered by element *)
           ; elf64_section_offset = 0 (* ignored -- will be replaced when file offsets are assigned *)
           ; elf64_section_size = 24 (* size of an Elf64_Rela *) * naturalFromNat n_iplt_entries (* ignored? NO, we use it in linker_script to avoid plumbing through the element record *)
           ; elf64_section_link = 0
           ; elf64_section_info = (* FIXME: want this to be the PLT section shndx *) 0
           ; elf64_section_align = 8
           ; elf64_section_entsize = 24
           ; elf64_section_body = Byte_sequence.empty (* ignored *)
           ; elf64_section_name_as_string = ".rela.iplt"
           |>
        )))
    }
    in
    <|  elements = Map.insert ".got" <|
                    startpos = Nothing
               ;    length = Just (got_nentries * got_entrysize)
               ;    contents = []
               |> (Map.insert ".plt" <|
                    startpos = Nothing
               ;    length = let len = 16 * total_n_plt_entries in 
                        (*let _ = errln ("PLT length in element: " ^ (show len) ^ " bytes")
                        in*) Just (16 * total_n_plt_entries)
               ;    contents = []
               |> (Map.insert ".rela.iplt" <|
                    startpos = Nothing
               ;    length = Just (24 (* size of an Elf64_Rela *) * naturalFromNat n_iplt_entries)
               ;    contents = []
               |> Map.empty
               ))
     ;   by_tag = by_tag_from_by_range new_by_range
     ;   by_range = new_by_range
     |>

val amd64_concretise_support : annotated_memory_image any_abi_feature -> annotated_memory_image any_abi_feature
let amd64_concretise_support orig_img = 
    (* Fill in the GOT contents. *)
    match amd64_find_got_label_and_element orig_img with
        Nothing -> orig_img (* no GOT, but that's okay *) 
        | Just (got_l, got_el) ->
    match amd64_find_plt_label_and_element orig_img with
        Nothing -> orig_img (* no PLT, but possibly a GOT -- is that okay? *)
        | Just (plt_l, plt_el) ->
    let got_base_addr = match got_el.startpos with
        Just a -> a
        | Nothing -> failwith "GOT has no address assigned"
    end
    in 
    let plt_base_addr = match plt_el.startpos with
        Just a -> a
        | Nothing -> failwith "PLT has no address assigned" 
    end
    in
    let got_entry_bytes_for = fun img -> fun symname -> fun maybe_def -> match maybe_def with
        Nothing -> replicate 8 (byte_of_natural 0)
        | Just sd ->
            (* What should the GOT slot be initialized to point to? 
             * If there's a PLT entry, we should point to that + 6,
             * i.e. the second instruction.
             * 
             * If there's not, then it might be a thread-local. *)
            match List.findIndex (fun (plt_symname, _, _) -> symname = plt_symname) plt_l with
                Just plt_slot_idx ->
                    match plt_el.startpos with
                        Just addr -> natural_to_le_byte_list_padded_to 8 (addr + ((naturalFromNat plt_slot_idx) * 16) + 6)
                        | Nothing -> failwith ("no PLT!")
                    end
                | Nothing -> 
                    (* Just look for a definition. *)
                        match Memory_image_orderings.find_defs_matching sd img with
                        [] -> failwith ("no matching definitions for GOT entry named " ^ symname)
                        | [(Just(def_el_name, (def_start, def_len)), d)] -> 
                            match element_and_offset_to_address (def_el_name, def_start) img with
                                Nothing -> failwith ("no address for definition offset in element " ^ def_el_name)
                                | Just x -> 
                                    (* If sd is a TLS symbol, we want its offset from the *end* of the 
                                     * TLS segment. *)
                                    (* FIXME: factor out this logic so that it lives in the TLS ABI spec. *)
                                    if get_elf64_symbol_type sd.def_syment = stt_tls then
                                        (* FIXME: the right way to do this is to mark the segments in the image 
                                         * *first*. They can't have ranges, because they span many elements,
                                         * but they can have vaddr ranges as arguments. *)
                                        let offs = i2n_signed 64 (0-8)
                                        in
                                        let _ = errln ("GOT slot for TLS symbol `" ^ symname ^ 
                                            "' created containing offset 0x" ^ (hex_string_of_natural offs))
                                        in
                                        natural_to_le_byte_list offs
                                    else let _ = errln ("GOT slot for symbol `" ^ symname ^ 
                                        "' created pointing to address 0x" ^ (hex_string_of_natural x) ^ 
                                        " (offset 0x" ^ (hex_string_of_natural def_start) ^ " in element " ^ def_el_name ^ ")")
                                    in
                                    natural_to_le_byte_list_padded_to 8 x
                            end
                        | _ -> failwith ("multiple matching definitions for GOT entry named " ^ symname)
                    end
            end
    end
    in
    let concretise_got = fun img -> 
        let l = got_l
        (*       Just(got_el_name, (got_start_off, got_len)))] ->   *)
        in
        let got_entry_contents = List.map (fun (symname, maybe_def) -> 
            List.map (fun b -> Just b) (got_entry_bytes_for img symname maybe_def)) l
        in
        (* We replace the GOT element's contents with the concrete addresses
         * of the symbols it should contain. We leave the metadata label in there,
         * for the relocation logic to find. If we change the order of entries, 
         * change it there too. *)
        let got_content = List.concat got_entry_contents
        in
        let new_got_el = 
            <| contents = got_content
             ; startpos = got_el.startpos
             ; length   = got_el.length
             |>
        in
        let new_got_tag = AbiFeature(Amd64AbiFeature(Abi_amd64.GOT(l)))
        in
        let got_range = Just(".got", (0, 8 * length l))
        in
        let new_by_tag = ((img.by_tag : set ((range_tag any_abi_feature) * (maybe element_range))) \
            {(AbiFeature(Amd64AbiFeature(Abi_amd64.GOT(l))), got_range)})
            union {(new_got_tag, got_range)}
        in
        let new_elements_map = Map.insert ".got" new_got_el (
            Map.delete ".got" img.elements
        )
        in
        <| elements = new_elements_map
         ; by_tag   = new_by_tag
         ; by_range = by_range_from_by_tag new_by_tag
         |>
    in
    let concretise_plt = fun img ->
       let l = plt_l
       in
        (* We replace the PLT element's contents with the concrete entries
         * for each of the symbols in the table. We leave the metadata label in there,
         * for the relocation logic to find. If we change the order of entries, 
         * change it there too. *)
        let all_content = concatMap (fun (_, _, plt_content_fn) -> 
            let (_, content) = plt_content_fn got_base_addr plt_base_addr in 
            content
        ) l
        in
        (*let _ = errln ("Got " ^ (show (length all_content)) ^ " bytes of PLT content")
        in*)
        (*let _ = errln ("Generated PLT reserved " ^ (show (match plt_el.length with
            Just n -> n
            | Nothing -> failwith "PLT has no length"
        end)) ^ " bytes of PLT content")
        in*)
        let new_plt_el = 
            <| contents = List.map (fun b -> Just b) all_content
             ; startpos = plt_el.startpos
             ; length   = Just(length all_content)
             |>
        in
        let new_elements_map = Map.insert ".plt" new_plt_el (
            Map.delete ".plt" img.elements
        )
        in
        <| elements = new_elements_map
         ; by_tag   = img.by_tag
         ; by_range = img.by_range
         |>
    in
    let concretise_rela_plt = fun img ->
        let maybe_rela_plt_el = Map.lookup ".rela.plt" img.elements
        in
        let maybe_new_rela_plt_el = match maybe_rela_plt_el with
            Nothing -> (* got no .rela.plt? okay... *) 
                (*let _ = errln "No .rela.plt found"
                in*)
                Nothing
            | Just rela_plt_el -> 
                let got_entry_iplt_widget_for = fun symname -> fun maybe_def -> match maybe_def with
                    Nothing -> Nothing
                    | Just sd -> 
                        if get_elf64_symbol_type sd.def_syment <> stt_gnu_ifunc then Nothing
                        else Just(fun index_in_got ->
                            (* This is a 24-byte Elf64_Rela. *)
                            let (r_offset : natural) (* GOT *slot* address! *) = 
                                match got_el.startpos with
                                    Nothing -> failwith "internal error: GOT has no assigned address"
                                    | Just addr -> addr + (8 * index_in_got)
                                end
                            in
                            let (r_info : natural) = r_x86_64_irelative in
                            (natural_to_le_byte_list_padded_to 8 r_offset ++
                            natural_to_le_byte_list_padded_to 8 r_info ++
                            (* r_addend -- address of the ifunc definition.
                             * NOTE that this is NOT the same as the GOT entry bytes.
                             * It's the actual address of the ifunc, whereas
                             * the GOT entry is initialized to point back into the PLT entry. *)
                            match Memory_image_orderings.find_defs_matching sd img with
                                  [] -> failwith ("impossible: IPLT entry widget found matching ifunc definition for " ^ symname)
                                | [(Just(def_el_name, (def_start, def_len)), d)] -> 
                                    match element_and_offset_to_address (def_el_name, def_start) img with
                                        Nothing -> failwith ("no address for ifunc definition offset in element " ^ def_el_name)
                                        | Just x -> 
                                            (* If sd is a TLS symbol, we want its offset from the *end* of the 
                                             * TLS segment. *)
                                            (* FIXME: factor out this logic so that it lives in the TLS ABI spec. *)
                                            if get_elf64_symbol_type sd.def_syment <> stt_gnu_ifunc
                                            then failwith "impossible: found ifunc definition that is not an ifunc"
                                            else
                                                natural_to_le_byte_list_padded_to 8 x
                                    end
                                | _ -> failwith "impossible: more than one ifunc definition"
                             end
                           ))
                        (* end Just sd *)
                    end
                in
                let rela_iplt_widgets = List.map (fun (symname, maybe_def) -> got_entry_iplt_widget_for symname maybe_def) got_l
                in
                let new_content_bytelists = 
                    mapi (fun i -> fun rela_widget -> 
                    match rela_widget with
                        Just f -> f (naturalFromNat i)
                        | Nothing -> []
                    end
                    ) rela_iplt_widgets
                in
                let new_contents = (List.map (fun b -> Just b) (List.concat new_content_bytelists))
                in
                (*let _ = errln ("Concretised .rela.plt; first 24 bytes: " ^ (show (take 24 new_contents)))
                in*)
                Just(
                    <| contents = new_contents
                     ; startpos = rela_plt_el.startpos
                     ; length   = rela_plt_el.length
                     |>
                )
        end
        in
        let new_elements_map = match maybe_new_rela_plt_el with
            Just new_rela_plt_el -> Map.insert ".rela.plt" new_rela_plt_el (
                Map.delete ".rela.plt" img.elements
            )
            | Nothing -> img.elements
        end
        in
        <| elements = new_elements_map
         ; by_tag   = img.by_tag
         ; by_range = img.by_range
         |>
    in
    concretise_rela_plt (concretise_plt (concretise_got orig_img))
end end

val amd64_got_slot_idx : annotated_memory_image any_abi_feature -> symbol_reference_and_reloc_site -> natural
let amd64_got_slot_idx img rr = 
    (*let _ = errln ("Looking up GOT slot for symbol " ^ rr.ref.ref_symname) in*)
    match Map.lookup ".got" img.elements with
        Nothing -> (* got no GOT? okay... *) failwith "got no GOT"
        | Just got_el -> 
            (* Find the GOT tag. *)
            let tags_and_ranges = Multimap.lookupBy Memory_image_orderings.tagEquiv (AbiFeature(Amd64AbiFeature(Abi_amd64.GOT([])))) img.by_tag
            in
            match tags_and_ranges with
                [] -> failwith "error: GOT element but no ABI feature GOT tag"
                | [(AbiFeature(Amd64AbiFeature(Abi_amd64.GOT(l))), Just(got_el_name, (got_start_off, got_len)))] -> 
                    (* Find the slot corresponding to rr, if we have one. *)
                    let got_addr = match got_el.startpos with Just addr -> addr | Nothing -> failwith "GOT has no addr at reloc time" end
                    in
                    match rr.maybe_def_bound_to with
                        Just (_, Just(d)) -> 
                            match List.findIndex (fun (symname, maybe_def) -> Just(d) = maybe_def) l with
                                Just idx -> naturalFromNat idx
                             |  Nothing -> failwith ("no GOT slot for reloc against `" ^ rr.ref.ref_symname ^ "'")
                            end
                        | Just(_, Nothing) -> (* HACK: look for the weak symname. Really want more (ref-based) labelling. *)
                            match List.findIndex (fun (symname, _) -> symname = rr.ref.ref_symname) l with
                                Just idx -> naturalFromNat idx
                             |  Nothing -> failwith ("no GOT slot for reloc against undefined symbol `" ^ rr.ref.ref_symname ^ "'")
                            end
                        | _ -> failwith "GOT: unbound def"
                    end
                | _ -> failwith "got bad GOT"
            end
    end

val amd64_got_slot_addr : annotated_memory_image any_abi_feature -> symbol_reference_and_reloc_site -> natural
let amd64_got_slot_addr img rr = 
    match Map.lookup ".got" img.elements with
        Nothing -> (* got no GOT? okay... *) failwith "got no GOT"
        | Just got_el -> 
            (* Find the GOT tag. *)
            let tags_and_ranges = Multimap.lookupBy Memory_image_orderings.tagEquiv (AbiFeature(Amd64AbiFeature(Abi_amd64.GOT([])))) img.by_tag
            in
            match tags_and_ranges with
                [] -> failwith "error: GOT element but no ABI feature GOT tag"
                | [(AbiFeature(Amd64AbiFeature(Abi_amd64.GOT(l))), Just(got_el_name, (got_start_off, got_len)))] -> 
                    (* Find the slot corresponding to rr, if we have one. *)
                    let got_addr = match got_el.startpos with Just addr -> addr | Nothing -> failwith "GOT has no addr at reloc time" end
                    in
                    8 * amd64_got_slot_idx img rr + got_addr
                | _ -> failwith "got bad GOT"
            end
    end

val amd64_plt_slot_addr : annotated_memory_image any_abi_feature -> symbol_reference_and_reloc_site -> natural
let amd64_plt_slot_addr img rr = 
    match Map.lookup ".plt" img.elements with
        Nothing -> (* got no PLT? okay... *) failwith "got no PLT"
        | Just plt_el -> 
            (* Find the PLT tag. *)
            let tags_and_ranges = Multimap.lookupBy Memory_image_orderings.tagEquiv (AbiFeature(Amd64AbiFeature(Abi_amd64.PLT([])))) img.by_tag
            in
            match tags_and_ranges with
                [] -> failwith "error: PLT element but no ABI feature PLT tag"
                | [(AbiFeature(Amd64AbiFeature(Abi_amd64.PLT(l))), Just(plt_el_name, (plt_start_off, plt_len)))] -> 
                    let plt_addr = match plt_el.startpos with Just addr -> addr | Nothing -> failwith "PLT has no addr at reloc time" end
                    in
                    (* Find the slot corresponding to rr, if we have one. *)
                    match rr.maybe_def_bound_to with
                        Just (_, Just(d)) -> 
                            match List.mapMaybe (fun (symname, maybe_def, fn) -> if Just(d) = maybe_def then Just fn else Nothing) l with
                                [fn] -> 
                                    let got_addr = 
                                         match Map.lookup ".got" img.elements with
                                            Nothing -> (* got no GOT? okay... *) failwith "got no GOT (applying PLT calculation)"
                                            | Just got_el -> match got_el.startpos with
                                                Just addr -> addr
                                                | Nothing -> failwith "concrete GOT has no addr"
                                                end
                                        end
                                    in
                                    let (addr, content) = fn plt_addr got_addr
                                    in
                                    addr
                                | [] -> (* failwith ("internal error: no PLT entry for reloc against `" ^ rr.ref.ref_symname ^ "'") *)
                                    (* If we got no PLT slot, we assume it's because the PLT entry was optimised out. 
                                     * So we just return the address of the symbol itself. *)
                                    let _ = errln ("No PLT entry for reloc against `" ^ rr.ref.ref_symname ^ 
                                        "', which we assume was optimised to avoid the GOT")
                                    in
                                    match Memory_image_orderings.find_defs_matching d img with
                                        [] -> 0 (* HMM -- should be an error? *)
                                        | [(Just(el_name, (start_off, len)), matching_d)] ->
                                            match element_and_offset_to_address (el_name, start_off) img with
                                                Just a -> a
                                                | Nothing -> failwith ("internal error: could not get address for PLT-short-circuited symbol `" ^ rr.ref.ref_symname ^ "'")
                                            end
                                        | _ -> failwith ("output image has multiple and/or no-location definitions to which via-PLT ref to `" ^ rr.ref.ref_symname ^ "' could resolve")
                                    end
                                | _ ->  failwith ("internal error: multiple PLT entries for reloc against `" ^ rr.ref.ref_symname ^ "'")
                            end
                        | Just(_, Nothing) -> (* weak, so 0 *) 0
                        | _ -> failwith "PLT: unbound def"
                    end
                | _ -> failwith "got bad PLT"
            end
    end

(** [amd64_reloc r] yields a function that applies relocations of type [r]. *)
val amd64_reloc : reloc_fn any_abi_feature
let amd64_reloc r = 
    match (string_of_amd64_relocation_type r) with
    | "R_X86_64_NONE" ->            (false, fun img -> (fun site_addr -> (fun rr -> (0, fun s -> fun a -> fun e -> e))))
    | "R_X86_64_64" ->              (true,  fun img -> (fun site_addr -> (fun rr -> (8, fun s -> fun a -> fun e -> i2n (n2i s + a)))))
    | "R_X86_64_PC32" ->            (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n_signed 32 (((n2i s) + a) - (n2i site_addr))))))
    | "R_X86_64_GOT32" ->           (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n_signed 32 ((n2i (amd64_got_slot_idx img rr)) + a)))))
    | "R_X86_64_PLT32" ->           (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n_signed 32 ((n2i (amd64_plt_slot_addr img rr)) + a - (n2i site_addr)) ))) )
    | "R_X86_64_COPY" ->            (false, fun img -> (fun site_addr -> (fun rr -> (size_of_copy_reloc img rr, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_GLOB_DAT" ->        (false, fun img -> (fun site_addr -> (fun rr -> (size_of_def rr, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_JUMP_SLOT" ->       (false, fun img -> (fun site_addr -> (fun rr -> (4 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_RELATIVE" ->        (true,  fun img -> (fun site_addr -> (fun rr -> (8, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_GOTPCREL" ->        (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n_signed 32 ((n2i (amd64_got_slot_addr img rr)) + a - (n2i site_addr)) ))) )
    | "R_X86_64_32" ->              (true,  fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n (n2i s + a)))))
    | "R_X86_64_32S" ->             (true,  fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n_signed 32 ((n2i s) + a)))))
    | "R_X86_64_16" ->              (true,  fun img -> (fun site_addr -> (fun rr -> (2, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_PC16" ->            (false, fun img -> (fun site_addr -> (fun rr -> (2, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_8" ->               (true,  fun img -> (fun site_addr -> (fun rr -> (1, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_PC8" ->             (false, fun img -> (fun site_addr -> (fun rr -> (1, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_DTPMOD64" ->        (false, fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_DTPOFF64" ->        (false, fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_TPOFF64" ->         (false, fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> i2n_signed 64 (0-8)  (* FIXME *)))))
    | "R_X86_64_TLSGD" ->           (false, fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_TLSLD" ->           (false, fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_DTPOFF32" ->        (false, fun img -> (fun site_addr -> (fun rr -> (4 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_GOTTPOFF" ->        (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> i2n_signed 32 ((n2i (amd64_got_slot_addr img rr)) + a - (n2i site_addr))))))
    | "R_X86_64_TPOFF32" ->         (false, fun img -> (fun site_addr -> (fun rr -> (4 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_PC64" ->            (false, fun img -> (fun site_addr -> (fun rr -> (8, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_GOTOFF64" ->        (false, fun img -> (fun site_addr -> (fun rr -> (8, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_GOTPC32" ->         (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_SIZE32" ->          (false, fun img -> (fun site_addr -> (fun rr -> (4, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_SIZE64" ->          (false, fun img -> (fun site_addr -> (fun rr -> (8, fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_GOTPC32_TLSDESC" -> (false, fun img -> (fun site_addr -> (fun rr -> (4 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_TLSDESC_CALL" ->    (false, fun img -> (fun site_addr -> (fun rr -> (4 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_TLSDESC" ->         (false, fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | "R_X86_64_IRELATIVE" ->       (true,  fun img -> (fun site_addr -> (fun rr -> (8 (* CHECK *), fun s -> fun a -> fun e -> e (* FIXME *)))))
    | _ -> failwith "unrecognised relocation"
end

val sysv_amd64_std_abi : abi any_abi_feature
let sysv_amd64_std_abi = 
   <| is_valid_elf_header = header_is_amd64
    ; make_elf_header = make_elf64_header elf_data_2lsb elf_osabi_none 0 elf_ma_x86_64
    ; reloc = amd64_reloc
    ; section_is_special = section_is_special
    ; section_is_large = (fun s -> (fun f -> flag_is_set shf_x86_64_large s.elf64_section_flags))
    ; maxpagesize = 65536
    ; minpagesize = 4096
    ; commonpagesize = 4096
      (* XXX: DPM, changed from explicit reference to null_abi field due to problems in HOL4. *)
    ; symbol_is_generated_by_linker = (fun symname -> symname = "_GLOBAL_OFFSET_TABLE_")
    ; make_phdrs = make_default_phdrs
    ; max_phnum = 2 (* incremented by extensions *)
    ; guess_entry_point = find_start_symbol_address
    ; pad_data = pad_zeroes
    ; pad_code = pad_0x90
    ; generate_support = amd64_generate_support
    ; concretise_support = amd64_concretise_support
    ; get_reloc_symaddr = amd64_get_reloc_symaddr
    |>

val sysv_aarch64_le_std_abi : abi any_abi_feature
let sysv_aarch64_le_std_abi = 
   <| is_valid_elf_header = header_is_aarch64_le
    ; make_elf_header = make_elf64_header elf_data_2lsb elf_osabi_none 0 elf_ma_aarch64
    ; reloc = aarch64_le_reloc
    ; section_is_special = section_is_special
    ; section_is_large = (fun _ -> (fun _ -> false))
    ; maxpagesize = 2 * 256 * 4096 (* 2MB; bit of a guess, based on gdb and prelink code *)
    ; minpagesize = 1024 (* bit of a guess again *)
    ; commonpagesize = 4096
    ; symbol_is_generated_by_linker = (fun symname -> symname = "_GLOBAL_OFFSET_TABLE_")
    ; make_phdrs = make_default_phdrs
    ; max_phnum = 2 (* incremented by extensions *)
    ; guess_entry_point = find_start_symbol_address
    ; pad_data = pad_zeroes
    ; pad_code = pad_zeroes
    ; generate_support = (* fun _ -> *) fun _ -> get_empty_memory_image ()
    ; concretise_support = fun img -> img
    ; get_reloc_symaddr = default_get_reloc_symaddr
    |>

val all_abis : list (abi any_abi_feature)
let all_abis = [sysv_amd64_std_abi; sysv_aarch64_le_std_abi; null_abi]

