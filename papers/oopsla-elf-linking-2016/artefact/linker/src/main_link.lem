open import Basic_classes
open import Function
open import String
open import Tuple
open import Bool
open import List
open import Sorting
import Map
import Set
import Set_extra
open import Num
open import Maybe
open import Assert_extra

open import Byte_sequence
open import Default_printing
open import Error
open import Missing_pervasives
open import Show
open import Endianness

open import Elf_header
open import Elf_file
open import Elf_interpreted_section
open import Elf_interpreted_segment
open import Elf_section_header_table
open import Elf_program_header_table
open import Elf_types_native_uint
open import Elf_relocation
open import String_table

open import Abi_amd64_elf_header
open import Abi_amd64_serialisation
open import Abis
import Gnu_ext_abi

open import Command_line
open import Input_list
open import Linkable_list

open import Memory_image
open import Elf_memory_image
open import Elf_memory_image_of_elf64_file
open import Elf64_file_of_elf_memory_image

open import Linker_script
open import Link

val images_consistent : elf_memory_image -> elf_memory_image -> bool
let images_consistent img1 img2 =
    (* img1.by_tag = img2.by_tag *) true

val correctly_linked : abi any_abi_feature -> linkable_list -> list string -> set link_option -> elf64_file -> maybe elf_memory_image
let correctly_linked a linkables names options eout = 
    let output_image = elf_memory_image_of_elf64_file a "(output file)" eout
    in
    let script = default_linker_control_script a 
        (* user_text_segment_start *) (match Command_line.find_option_matching_tag (TextSegmentStart(0)) options with Just(TextSegmentStart(addr)) -> Just addr | _ -> Nothing end)
        (* user_data_segment_start *) Nothing
        (* user_rodata_segment_start *) (match Command_line.find_option_matching_tag (RodataSegmentStart(0)) options with Just(RodataSegmentStart(addr)) -> Just addr | _ -> Nothing end)
        (* elf_headers_size *)
            (
                (* ELF header size *) 64 + a.max_phnum * 56 (* size of one phdr *)
            )
    in
    let linked_image = link script a options linkables
    in
    if images_consistent output_image linked_image then Just linked_image else Nothing

(* We need to elaborate the command line to handle objects, archives 
 * and archive groups appropriately. 
 * We could imagine a relation between objects such that 
 * (o1, o2) is in the relation
 * iff definitions in o1 might be used to satisfy references in o2. ("o1 supplies o2")
 * If o1 is a .o, all other .o files are searched.
 * If o1 comes from an archive and is not in a group, it only supplies *preceding* objects (whether from an archive or a .o).
 * If o1 comes from an archive in a group, it supplies preceding objects and any objects from the same group.
 * 
 * That doesn't capture the ordering, though: 
 * for each object, there's an ordered list of other objects 
 * in which to search for the *first* definition. *)

let _ =
  let res =
    let (input_units, link_options) = command_line ()
    in
    let items_and_options = elaborate_input input_units
    in
    let (input_items, item_options) = unzip items_and_options
    in
    let _ = Missing_pervasives.errln ("Got " ^ (show (List.length input_items)) ^ " input items: {"
        ^ (List.foldl (^) "" (List.map (fun item -> (show item) ^ ",\n") input_items)) ^ "}")
    in
    let output_filename = match Command_line.find_option_matching_tag (Command_line.OutputFilename("")) link_options with
        Nothing -> "impossible: no output file specified, despite default value of `a.out'"
        | Just (Command_line.OutputFilename(s)) -> s
        | _ -> "impossible: bad output filename option returned"
    end
    in
    Byte_sequence.acquire output_filename >>= fun out ->
    let _ = Missing_pervasives.errln ("Successfully opened output file") in
    Elf_file.read_elf64_file out >>= fun eout ->
    let _ = Missing_pervasives.errln ("Output file seems to be an ELF file") in
    let guessed_abi = find (fun a -> a.is_valid_elf_header eout.elf64_file_header) all_abis
    in
    let a = match guessed_abi with
      Just a -> if (* get_elf64_osabi eout.elf64_file_header = elf_osabi_gnu *) true
                    (* The GNU linker does not set the ABI to "GNU", but happily uses GNU extensions.
                     * FIXME: delegate to a personality function here
                     *)
                    then let _ = Missing_pervasives.errln "Using GNU-extended ABI" in Gnu_ext_abi.gnu_extend (Abis.tls_extend a) 
                    else (Abis.tls_extend a)
      | Nothing -> failwith "output file does not conform to any known ABI"
    end
    in
    let make_linkable = fun (it, opts) -> linkable_item_of_input_item_and_options a it opts
    in
    let linkable_items_and_options = List.map make_linkable items_and_options
    in
    let names = List.map show input_items
    in 
    let maybe_symbolic_image = correctly_linked a linkable_items_and_options names link_options eout
    in
    let v = match maybe_symbolic_image with 
        Nothing -> false 
        | Just img ->
            (* generate some output, using the symbolic image we just got *)
            let our_output_filename = output_filename ^ ".test-out"
            in
            let f = elf64_file_of_elf_memory_image a (fun x -> x) our_output_filename img
            in
            match 
            bytes_of_elf64_file f >>= fun bytes -> 
                Byte_sequence.serialise our_output_filename bytes
            with
                Success _ -> true
                | Fail s -> let _ = outln ("error writing output: " ^ s) in true
            end
    end
    in
    return (show v)
  in
    match res with
      | Fail err  -> Missing_pervasives.errln ("[!]: " ^ err)
      | Success e -> Missing_pervasives.errln e
    end


